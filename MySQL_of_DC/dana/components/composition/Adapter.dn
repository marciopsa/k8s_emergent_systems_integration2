uses AdaptEvents

component provides Adapter {
	
	void adaptStatelessObject(IDC ofComponent, Object object, IDC source, char type[])
		{
		AdaptEvents ae
		if (dana.pauseObject(ofComponent, object))
			{
			// - construct a new object
			Object a = dana.constructObject(source :< type)
			// - notify the object that it's now the "inactive" copy
			if ((ae = dana.getObjectInterface(ofComponent, object, AdaptEvents)) != null) ae.inactive()
			// - rewire live object so calls now go to the new one (a becomes null)
			Object b = dana.rewireObject(object, a)
			// - notify the object that it's now the "active" copy
			if ((ae = dana.getObjectInterface(source, object, AdaptEvents)) != null) ae.active()
			// - allow new calls to proceed in the new object
			dana.resumeObject(object)
			// - wait for all in-progress calls to finish in the old object
			dana.waitForObject(b)
			// - wait for any in-progress asynchronous threads to finish
			dana.waitForObjectThreads(b)
			}
		}
	
	void adaptStatefulObject(IDC ofComponent, Object object, IDC source, char type[])
		{
		AdaptEvents ae
		if (dana.pauseObject(ofComponent, object))
			{
			// - wait for all in-progress calls to finish in the old object
			dana.waitForObject(object)
			// - construct a new object
			Object a = dana.constructObject(source :< type)
			// - notify the object that it's now the "inactive" copy
			if ((ae = dana.getObjectInterface(ofComponent, object, AdaptEvents)) != null) ae.inactive()
			// - get the object's state
			Data transferState = dana.getObjectState(ofComponent, object)
			// - rewire live object so calls now go to the new one (a becomes null)
			Object b = dana.rewireObject(object, a)
			// - set the object's state
			dana.setObjectState(source, object, transferState)
			// - notify the object that it's now the "active" copy
			if ((ae = dana.getObjectInterface(source, object, AdaptEvents)) != null) ae.active()
			// - allow new calls to proceed in the new object
			dana.resumeObject(object)
			// - wait for any in-progress asynchronous threads to finish
			dana.waitForObjectThreads(b)
			}
		}
	
	bool Adapter:adaptObject(IDC ofComponent, Object object, IDC newImplementation, char typeName[])
		{
		if (dana.hasTransferState(object))
			adaptStatefulObject(ofComponent, object, newImplementation, typeName)
			else
			adaptStatelessObject(ofComponent, object, newImplementation, typeName)
		
		return true
		}
	
	bool Adapter:adaptRequiredInterface(IDC ofComponent, char interfaceName[], IDC toComponent)
		{
		if (ofComponent :> interfaceName != null)
			{
			IDC oldTarget = dana.getComponent(ofComponent :> interfaceName)
			
			//temporarily prevent new object instances being created or destroyed, and snapshot the current set of objects
			dana.pause(ofComponent :> interfaceName)
			//switch the binding from this role to the new component (all new objects will be sourced from the new component)
			dana.rewire(ofComponent :> interfaceName, toComponent :< interfaceName)
			Object objects[] = dana.getObjects(ofComponent :> interfaceName)
			dana.resume(ofComponent :> interfaceName)
			
			//switch all existing objects to the new class
			for (int i = 0; i < objects.arrayLength; i++)
				{
				adaptObject(oldTarget, objects[i], toComponent, interfaceName)
				}
			}
			else
			{
			throw new Exception("No bindport of type '$interfaceName' found on component")
			}
		
		return true
		}
	
	}