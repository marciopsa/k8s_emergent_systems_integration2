data ListItem{
	Data item
	ListItem next
	ListItem prev
	}

component provides Queue(Destructor, AdaptEvents) {
	
	ListItem list
	ListItem listEnd
	int length
	Mutex listLock = new Mutex()
	
	void Queue:add(Data item)
		{
		mutex(listLock)
			{
			ListItem ni = new ListItem(item)
			
			if (list == null)
				{
				list = ni
				}
				else
				{
				listEnd.next = ni
				ni.prev = listEnd
				}
			
			listEnd = ni
			
			length ++
			}
		}
	
	void Queue:remove(Data item)
		{
		mutex(listLock)
			{
			ListItem lw = list
			while (lw != null)
				{
				if (lw.item === item)
					{
					if (lw.prev != null)
						lw.prev.next = lw.next
						else
						list = lw.next
					
					if (lw.next != null)
						lw.next.prev = lw.prev
						else
						listEnd = lw.prev
					
					lw.next = null
					lw.prev = null
					
					break
					}
				
				lw = lw.next
				}
			
			length --
			}
		}
	
	void Queue:update(Data item, Data newVersion)
		{
		mutex(listLock)
			{
			ListItem lw = list
			while (lw != null)
				{
				if (lw.item === item)
					{
					lw.item = newVersion
					
					break
					}
				
				lw = lw.next
				}
			}
		}
	
	Data Queue:service()
		{
		mutex(listLock)
			{
			if (list != null)
				{
				ListItem current = list
				
				list = list.next
				
				current.next = null
				
				if (list != null)
					{
					list.prev = null
					}
					else
					{
					listEnd = null
					}
				
				length --
				
				return current.item
				}
				else
				{
				return null
				}
			}
		}
	
	Data Queue:peek()
		{
		mutex(listLock)
			{
			if (list != null)
				return list.item
				else
				return null
			}
		}
	
	int Queue:getLength()
		{
		mutex(listLock)
			{
			return length
			}
		}
	
	Data[] Queue:getContents()
		{
		mutex(listLock)
			{
			Data items[] = new Data[length]
			
			int j = 0
			for (ListItem i = list; i != null; i = i.next)
				{
				items[j] = i.item
				j ++
				}
			
			return items
			}
		}
	
	void buildFromArray(Data items[])
		{
		clearList()
		
		for (int i = 0; i < items.arrayLength; i++)
			{
			add(items[i])
			}
		}
	
	bool Queue:clone(Object o)
		{
		Queue src = o
		
		Data items[] = src.getContents()
		
		buildFromArray(items)
		
		return true
		}
	
	void clearList()
		{
		ListItem lw = list
		list = null
		listEnd = null
		while (lw != null)
			{
			ListItem td = lw
			
			lw = lw.next
			
			td.next = null
			td.prev = null
			}
		
		length = 0
		}
	
	void Destructor:destroy()
		{
		clearList()
		}
	
	void AdaptEvents:inactive()
		{
		//flatten list contents to transfer state
		content = getContents()
		}
	
	void AdaptEvents:active()
		{
		//unpack list contents from transfer state
		buildFromArray(content)
		}
	
	}