component provides FileObject(AdaptEvents) requires io.FileSystem fileSystem, Font, os.SystemInfo sysInfo, data.StringUtil stringUtil, io.Output out {
	
	Font labelFont
	char label[]
	
	FileObject:FileObject(char f[])
		{
		labelFont = new Font(sysInfo.getSystemFont(false), 15)
		
		setFile(f)
		}
	
	void AdaptEvents:active()
		{
		FileObject(filePath)
		postRepaint()
		}
	
	char[] getForWidth(char a[], int width)
		{
		char res[] = a
		int subLet = a.arrayLength - 1
		
		while (labelFont.getTextWidth(res) > width)
			{
			subLet --
			res = new char[](subString(a, 0, subLet), "...")
			}
		
		return res
		}
	
	char[] subString(char array[], int start, int length)
		{
		char result[]
		
		for (int i = 0; i < length; i++)
			result = new char[](result, array[start+i])
		
		return result
		}
	
	char[] normalisePath(char path[])
		{
		int nxt = 0
		while ((nxt = stringUtil.find(path, "\\")) != StringUtil.NOT_FOUND)
			{
			path = new char[](subString(path, 0, nxt), "/", subString(path, nxt+1, path.arrayLength - (nxt+1)))
			}
		return path
		}
	
	void FileObject:setFile(char f[])
		{
		filePath = normalisePath(f)
		
		String parts[] = stringUtil.explode(filePath, "/")
		
		label = getForWidth(parts[parts.arrayLength-1].string, 46)
		}
	
	char[] FileObject:getFile()
		{
		return filePath
		}
	
	void FileObject:setPosition(int x, int y)
		{
		xPosition = x
		yPosition = y
		}
	
	void FileObject:paint(Canvas c)
		{
		FileInfo fi = fileSystem.getInfo(filePath)
		
		if (fi.type == FileInfo.TYPE_DIR)
			c.drawRect(new Rect2D(xPosition, yPosition, 40, 44, new Color(157, 188, 220, 255)))
			else
			c.drawRect(new Rect2D(xPosition, yPosition, 40, 44, new Color(250, 245, 255, 255)))
		
		c.drawText(new Point2D(xPosition, yPosition + 47, new Color(100, 100, 100, 255)), labelFont, label)
		}
	
	Rect FileObject:getBounds()
		{
		return new Rect(xPosition, yPosition, 40, 60)
		}
	
	Point FileObject:getPosition()
		{
		return new Point(xPosition, yPosition)
		}
	
	void FileObject:click(int x, int y, int button)
		{
		if (button == MouseButtons.BUTTON_LEFT)
			{
			emitevent click()
			}
		}
	
	WH FileObject:getPreferredSize()
		{
		return new WH(40, 60)
		}
	
	void FileObject:postRepaint()
		{
		emitevent repaint()
		}
	
	}