uses events.EventData

component provides App requires ui.Window, ui.Button, io.Output out {
	
	Window window
	Button b1
	Button b2
	
	Thread main
	
	eventsink AppEvents(EventData ed)
		{
		if (ed.type == Button.[click] && ed.source === b1)
			{
			out.println("A BUTTON CLICK!")
			}
			else if (ed.type == Button.[click] && ed.source === b2)
			{
			out.println("A BUTTON CLICK TOO!")
			}
			else if (ed.source === window && ed.type == Window.[close])
			{
			main.signal()
			}
		}
	
	int App:main(AppParam params[])
		{
		window = new Window("MyWindow")
		window.setSize(250, 250)
		window.setVisible(true)
		
		b1 = new Button("One")
		b2 = new Button("Two")
		
		b1.setPosition(10, 30)
		b2.setPosition(70, 30)
		
		window.addObject(b1)
		window.addObject(b2)
		
		sinkevent AppEvents(b1)
		sinkevent AppEvents(b2)
		
		sinkevent AppEvents(window)
		
		main = this.thread
		this.thread.wait()
		
		window.close()
		
		return 0
		}
	
	}